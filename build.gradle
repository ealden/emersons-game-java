plugins {
    id 'application'
    id 'war'
    id 'jacoco'
    id 'org.sonarqube' version '3.+'
}

group 'com.escanan.ealden.race'
version '1.0.0-SNAPSHOT'

configurations {
    cucumberRuntime {
        extendsFrom testImplementation
    }
    developmentOnly
    runtimeClasspath {
        extendsFrom developmentOnly
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.apache.tomcat.embed:tomcat-embed-core:8.5.+'
    implementation 'org.apache.tomcat.embed:tomcat-embed-jasper:8.5.+'
    implementation 'com.google.code.gson:gson:2.8.+'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.+'
    testImplementation 'io.cucumber:cucumber-java:7.+'
    testImplementation 'io.cucumber:cucumber-junit:7.+'
    testImplementation 'org.seleniumhq.selenium:selenium-java:4.0.+'
    testImplementation 'org.hamcrest:hamcrest:2.+'
    testImplementation 'org.mockito:mockito-core:4.+'
    testImplementation 'org.mockito:mockito-junit-jupiter:4.+'
    testImplementation 'io.github.bonigarcia:webdrivermanager:5.+'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.+'
}

application {
    mainClass = 'com.escanan.ealden.race.EmersonsGame'
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
}

test {
    finalizedBy jacocoTestReport

    useJUnitPlatform()
}

jacocoTestReport {
    dependsOn test

    reports {
        xml.required = true
    }
}

sonarqube {
    properties {
        property 'sonar.exclusions', '**/EmersonsGame.java'

        property 'sonar.projectKey', 'ealden_emersons-game-java'
        property 'sonar.projectName', 'emersons-game-java'
        property 'sonar.organization', 'ealden'
        property 'sonar.host.url', 'https://sonarcloud.io'
    }
}

task cucumber(type: JavaExec) {
    dependsOn assemble, compileTestJava

    def feature = 'src/test/resources'

    if (project.hasProperty('feature')) {
        feature = project.getProperty('feature')
    }

    mainClass = 'io.cucumber.core.cli.Main'
    classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
    args = ['--tags', 'not @wip', '--plugin', 'pretty', '--glue', 'com.escanan.ealden.race', feature]
}

task stage() {
    dependsOn installDist
}
