plugins {
    id 'application'
    id 'war'
    id 'jacoco'
    id 'org.sonarqube' version '3.+'
}

group 'com.escanan.ealden.race'
version '1.0.0-SNAPSHOT'

configurations {
    cucumberRuntime {
        extendsFrom testImplementation
    }
    developmentOnly
    runtimeClasspath {
        extendsFrom developmentOnly
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.apache.tomcat.embed:tomcat-embed-core:10.1.+'
    implementation 'org.apache.tomcat.embed:tomcat-embed-jasper:10.1.+'
    implementation 'com.google.code.gson:gson:2.+'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.+'
    testImplementation 'io.cucumber:cucumber-java:7.+'
    testImplementation 'org.seleniumhq.selenium:selenium-java:4.+'
    testImplementation 'org.hamcrest:hamcrest:2.+'
    testImplementation 'org.mockito:mockito-core:4.+'
    testImplementation 'org.mockito:mockito-junit-jupiter:4.+'
    testImplementation 'io.github.bonigarcia:webdrivermanager:5.+'
}

application {
    mainClass = 'com.escanan.ealden.race.EmersonsGame'
}

java {
    sourceCompatibility = JavaLanguageVersion.of(21)
}

test {
    finalizedBy jacocoTestReport

    useJUnitPlatform()
}

jacocoTestReport {
    dependsOn test

    reports {
        xml.required = true
    }
}

sonarqube {
    properties {
        property 'sonar.exclusions', '**/EmersonsGame.java'

        property 'sonar.projectKey', 'ealden_emersons-game-java'
        property 'sonar.projectName', 'emersons-game-java'
        property 'sonar.organization', 'ealden'
        property 'sonar.host.url', 'https://sonarcloud.io'
    }
}

tasks.register('cucumber', JavaExec) {
    dependsOn build

    def feature = 'src/test/resources'

    if (project.hasProperty('feature')) {
        feature = project.getProperty('feature')
    }

    mainClass = 'io.cucumber.core.cli.Main'
    classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
    args = ['--tags', 'not @wip', '--plugin', 'pretty', '--glue', project.group, feature]
}

tasks.register('stage') {
    dependsOn installDist
}
