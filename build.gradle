plugins {
    id 'application'
    id 'java'
    id 'war'
}

group 'com.escanan.ealden.race'
version '1.0.0-SNAPSHOT'

configurations {
    cucumberRuntime {
        extendsFrom testImplementation
    }
    developmentOnly
    runtimeClasspath {
        extendsFrom developmentOnly
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.apache.tomcat.embed:tomcat-embed-core:8.5.65'
    implementation 'org.apache.tomcat.embed:tomcat-embed-jasper:8.5.65'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    testImplementation 'io.cucumber:cucumber-java:6.10.3'
    testImplementation 'io.cucumber:cucumber-junit:6.10.3'
    testImplementation 'org.seleniumhq.selenium:selenium-java:3.141.59'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
}

test {
    useJUnitPlatform()
}

task cucumber() {
    dependsOn assemble, compileTestJava

    doLast {
        def feature = 'src/test/resources'

        if (project.hasProperty('feature')) {
            feature = project.getProperty('feature')
        }

        javaexec {
            main = 'io.cucumber.core.cli.Main'
            classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
            args = ['--tags', 'not @wip', '--plugin', 'pretty', '--glue', 'com.escanan.ealden.race', feature]
        }
    }
}

application {
    mainClass = 'com.escanan.ealden.race.EmersonsGame'
}
